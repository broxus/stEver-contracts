pragma ever-solidity >=0.62.0;
pragma AbiHeader expire;
pragma AbiHeader pubkey;

import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";
import "broxus-ton-tokens-contracts/contracts/interfaces/IAcceptTokensTransferCallback.tsol";
import "broxus-ton-tokens-contracts/contracts/interfaces/ITokenRoot.tsol";
import "broxus-ton-tokens-contracts/contracts/interfaces/ITokenWallet.tsol";
import "broxus-ton-tokens-contracts/contracts/interfaces/IDestroyable.tsol";

import "./interfaces/IStEverCluster.tsol";
import "./interfaces/IStEverVault.tsol";
import "./interfaces/IDepoolStrategyFactory.tsol";
import "./utils/Gas.tsol";
import "./utils/ErrorCodes.tsol";

/**
 * @title StEverCluster
 *
 * @dev Implementation of the {IStEverCluster} interface,
 * extended with the functionality of accepting incoming token transfers.
 *
 * This contract aggregates strategies owned by a validator and is crucial
 * for a validator's participation in the StEver project.
 *
 * It manages the deployment and delegation of strategies, assurance provision
 * by the `clusterOwner`, and the addition of strategies to the `StEverVault`.
 */
contract StEverCluster is IStEverCluster, IAcceptTokensTransferCallback {

    uint32 _nonce;
    address stEverVault;
    address clusterOwner;
    address stEverOwner;

    uint128 requiredAssurance;
    uint32 maxStrategiesCount;
    uint32 currentVersion;
    address strategyFactory;

    uint128 currentAssurance;
    ClusterState clusterState;

    address stEverTokenRoot;
    address stEverTokenWallet;

    bool isPunished;

    mapping(address => Strategy) public strategies;
    mapping(address => bool) public deployedStrategies;

    /**
     * @dev Constructor for the StEverCluster contract.
     * This constructor is empty and will revert if called.
     */
    constructor() public {revert();}

    /**
     * @dev Internal function to initialize the StEverCluster contract.
     * @param _version The version of the StEverCluster contract.
     * @param _assurance The assurance amount for the cluster.
     * @param _maxStrategiesCount The maximum number of strategies that can be added to the cluster.
     * @param _strategyFactory The address of the strategy factory contract.
     * @param _stEverTokenRoot The address of the StEver token root contract.
     * @param _stEverOwner The address of the StEver owner.
     *
     * **Preconditions**:
     *
     *  - The caller must be the owner.
     *  - The value of the message must be greater than or equal to the sum of DEPLOY_CLUSTER_VALUE and MIN_CALL_MSG_VALUE.
     *
     * **Postconditions**:
     *
     *  - A new cluster is created and added to the clusterPools.
     *  - The ClusterCreated event is emitted.
     */
    function _init(
        uint32 _version,
        uint128 _assurance,
        uint32 _maxStrategiesCount,
        address _strategyFactory,
        address _stEverTokenRoot,
        address _stEverOwner
    ) internal {
        requiredAssurance = _assurance;
        maxStrategiesCount = _maxStrategiesCount;
        currentVersion = _version;
        strategyFactory = _strategyFactory;
        stEverTokenRoot = _stEverTokenRoot;
        clusterState = ClusterState.INITIAL;
        stEverOwner = _stEverOwner;

        ITokenRoot(stEverTokenRoot).deployWallet{
                value: StEverVaultGas.ST_EVER_WALLET_DEPLOY_VALUE,
                callback: StEverCluster.handleStEverTokenWalletAddress,
                bounce: false
        }(address(this), StEverVaultGas.ST_EVER_WALLET_DEPLOY_GRAMS_VALUE);
    }

    /**
     * @dev Internal function to reserve the minimum balance for the contract.
     * @return The minimum balance for the contract.
     */
    function _reserve() internal pure returns (uint128) {
        return math.max(address(this).balance - msg.value, ClusterGas.CONTRACT_MIN_BALANCE);
    }

    /**
     * @dev Modifier to check if the caller is the StEverVault contract.
     */
    modifier onlyStEverVault() {
        require(msg.sender == stEverVault,ErrorCodesCluster.NOT_ST_EVER_VAULT);
        _;
    }
    /**
     * @dev Modifier to check if the caller is the StEverOwner.
     */
    modifier onlyStEverOwner() {
        require(msg.sender == stEverOwner,ErrorCodesCluster.NOT_ST_EVER_OWNER);
        _;
    }

    /**
     * @dev Modifier to check if the caller is the clusterOwner.
     */
    modifier onlyClusterOwner() {
        require(msg.sender == clusterOwner, ErrorCodesCluster.NOT_CLUSTER_OWNER);
        _;
    }
    /**
     * @dev Modifier to check if the caller is the StEverOwner or the clusterOwner.
     */
    modifier stEverOwnerOrClusterOwner() {
        require(msg.sender == stEverOwner || msg.sender == clusterOwner, ErrorCodesCluster.NOT_ST_EVER_OWNER_OR_CLUSTER_OWNER);
        _;
    }

    /**
     * @dev Modifier to check if the caller is the strategyFactory.
     */
    modifier onlyStrategyFactory() {
        require(msg.sender == strategyFactory, ErrorCodesCluster.NOT_STRATEGY_FACTORY);
        _;
    }
    /**
     * @dev Modifier to check if the value of the message is greater than or equal to the minimum call value.
     */
    modifier minCallValue() {
        require (msg.value >= ClusterGas.MIN_CALL_VALUE, ErrorCodesCluster.LOW_MSG_VALUE);
        _;
    }

    /**
     * @dev Function to handle the StEver token wallet address.
     * @param _tokenWallet The address of the token wallet.
     *
     * **Preconditions**:
     *
     *  - The caller must be the StEver token root contract.
     */
    function handleStEverTokenWalletAddress(address _tokenWallet) public {
        require (msg.sender == stEverTokenRoot, ErrorCodesCluster.NOT_ST_EVER_ROOT);
        stEverTokenWallet = _tokenWallet;
    }

    /**
     * @dev Sets StEverOwner address.
     * @param _newStEverOwner Address of new owner.
     * **Preconditions**:
     *
     *  - The caller must be the `StEverVault`
     *
     * **Postconditions**:
     *
     *  - stEverOwner updated with new address.
     */
    function setStEverOwner(address _newStEverOwner) external override onlyStEverVault {
        tvm.rawReserve(_reserve(), 0);
        stEverOwner = _newStEverOwner;
        clusterOwner.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }

    /**
     * @dev See {IStEverCluster-deployStrategies}.
     *
     * **Preconditions**:
     *
     *  - The caller must be the clusterOwner.
     *  - The value of the message must be greater than or equal to the required message value.
     *
     * **Postconditions**:
     *
     *  - Calls the {IDepoolStrategyFactory-deployStrategy} function of the strategy
     *    factory contract for each DePool.
     */
    function deployStrategies(address[] _dePools) override external onlyClusterOwner {
        uint128 valueForDeployOneStrategy = ClusterGas.STRATEGY_DEPLOY_VALUE  + ClusterGas.MIN_CALL_VALUE;
        uint128 requiredMsgValue = valueForDeployOneStrategy * uint128(_dePools.length) + ClusterGas.MIN_CALL_VALUE;
        require (msg.value >= requiredMsgValue, ErrorCodesCluster.LOW_MSG_VALUE);
        tvm.rawReserve(_reserve(), 0);

        for(address dePool : _dePools) {
            IDepoolStrategyFactory(strategyFactory).deployStrategy{
                value: valueForDeployOneStrategy,
                flag: MsgFlag.SENDER_PAYS_FEES,
                bounce: false
            }(dePool);
        }
        clusterOwner.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }

    /**
     * @dev See {IStEverCluster-onStrategyDeployed}.
     *
     * **Preconditions**:
     *
     *  - The caller must be the {DepoolStrategyFactory}.
     *
     * **Postconditions**:
     *
     *  - The deployed status of the strategy is set to true.
     *  - The NewStrategyDeployed event is emitted.
     */
    function onStrategyDeployed(address _strategy) override external onlyStrategyFactory {
        tvm.rawReserve(_reserve(), 0);

        deployedStrategies[_strategy] = true;

        emit NewStrategyDeployed(_strategy);

        clusterOwner.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }

    /**
     * @dev Adds strategies to the cluster.
     * This function checks if the assurance is sufficient and if the strategies limit is not reached.
     * It then checks if each strategy is deployed and not already added.
     * Finally, it calls the addStrategies function of the StEverVault contract.
     * @param _strategies The addresses of the strategies.
     *
     * **Preconditions**:
     *
     *  - The caller must be the clusterOwner.
     *  - The assurance must be sufficient.
     *  - The strategies limit must not be reached.
     *  - Each strategy must be deployed and not already added.
     *  - The value of the message must be greater than or equal to the required message value.
     *
     * **Postconditions**:
     *
     *  - The strategies are added to the StEverVault.
     */
    function addStrategies(address[] _strategies) public view onlyClusterOwner {
        if (currentAssurance < requiredAssurance) {
            revert(ErrorCodesCluster.ASSURANCE_NOT_ENOUGH);
        }

        if (strategies.keys().length + _strategies.length > maxStrategiesCount) {
            revert(ErrorCodesCluster.STRATEGIES_LIMIT_REACHED);
        }

        require (
            msg.value >= _strategies.length * StEverVaultGas.EXPERIMENTAL_FEE + StEverVaultGas.MIN_CALL_MSG_VALUE,
            ErrorCodes.NOT_ENOUGH_VALUE
        );

        uint8 batchSize = 50;

        require (_strategies.length <= batchSize, ErrorCodes.MAX_BATCH_SIZE_REACHED);

        tvm.rawReserve(_reserve(), 0);

        for (address strategy : _strategies) {

            if (strategies.exists(strategy)) {
                revert(ErrorCodesCluster.STRATEGY_ALREADY_EXISTS);
            }
            if (!deployedStrategies.exists(strategy)) {
                revert(ErrorCodesCluster.STRATEGY_NOT_DEPLOYED_BY_CLUSTER);
            }
        }

        IStEverVault(stEverVault).addStrategies{
            value: uint128(_strategies.length) * StEverVaultGas.EXPERIMENTAL_FEE,
            flag: MsgFlag.SENDER_PAYS_FEES,
            bounce: false
        }(_strategies, clusterOwner, _nonce);

        clusterOwner.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }

    /**
     * @dev See {IStEverCluster-onStrategiesAdded}.
     *
     * **Preconditions**:
     *
     *  - The caller must be the {StEverVault} contract.
     *
     * **Postconditions**:
     *
     *  - The state of each strategy is set to ACTIVE.
     */
    function onStrategiesAdded(address[] _strategies) override external onlyStEverVault {
        tvm.rawReserve(_reserve(), 0);

        for (address strategy : _strategies) {
            strategies[strategy].state = StrategyState.ACTIVE;
        }

        clusterOwner.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }

    /**
     * @dev Removes strategies from the cluster.
     * This function checks if each strategy exists and is in the ACTIVE state.
     * It then calls the _removeStrategies internal function.
     * @param _strategies The addresses of the strategies.
     *
     * **Preconditions**:
     *
     *  - The caller must be the StEverOwner or the clusterOwner.
     *  - Each strategy must exist and be in the ACTIVE state.
     *  - The value of the message must be greater than or equal to the required message value.
     *
     * **Postconditions**:
     *
     *  - The strategies are removed from the cluster.
     */
    function removeStrategies(address[] _strategies) public stEverOwnerOrClusterOwner {

        require(
            msg.value >= uint128(_strategies.length) * (StEverVaultGas.REMOVE_STRATEGY_RESERVE + StEverVaultGas.EXPERIMENTAL_FEE),
            ErrorCodesCluster.NOT_ENOUGH_VALUE
        );

        for (address strategy : _strategies) {
            require(strategies.exists(strategy), ErrorCodesCluster.STRATEGY_NOT_EXISTS);
            require(strategies[strategy].state == StrategyState.ACTIVE, ErrorCodesCluster.STRATEGY_NOT_IN_ACTIVE_STATE);
        }

        _removeStrategies(_strategies);
    }

    /**
     * @dev Internal function to remove strategies from the cluster.
     * This function sets the state of each strategy to REMOVING and calls the removeStrategies function of the StEverVault contract.
     * @param _strategies The addresses of the strategies.
     *
     * **Preconditions**:
     *
     *  - Each strategy must exist and be in the ACTIVE state.
     *
     * **Postconditions**:
     *
     *  - The state of each strategy is set to REMOVING.
     *  - The removeStrategies function of the StEverVault contract is called.
     */
    function _removeStrategies(address[] _strategies) internal {
        tvm.rawReserve(_reserve(), 0);

        for (address strategy : _strategies) {
            strategies[strategy].state = StrategyState.REMOVING;
        }

        IStEverVault(stEverVault).removeStrategies{
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED,
            bounce: false
        }(_strategies, clusterOwner, _nonce);
    }

    /**
     * @dev See {IStEverCluster-onPendingStrategyRemove}.
     *
     * **Preconditions**:
     *
     *  - The caller must be the {StEverVault} contract.
     *
     * **Postconditions**:
     *
     *  - The state of each strategy is set to PENDING_REMOVE.
     */
    function onPendingStrategyRemove(address[] _strategies) override external onlyStEverVault {
        tvm.rawReserve(_reserve(), 0);

        for (address strategy : _strategies) {
            strategies[strategy].state = StrategyState.PENDING_REMOVE;
        }

        address remainingGasTo = isPunished ? stEverOwner : clusterOwner;

        remainingGasTo.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }

    /**
     * @dev See {IStEverCluster-onStrategyRemoved}.
     *
     * **Preconditions**:
     *
     *  - The caller must be the {StEverVault} contract.
     *
     * **Postconditions**:
     *
     *  - The strategy is removed from the strategies mapping.
     *  - If the cluster is in the REMOVING state and there are no more strategies, the {IStEverVault-onClusterRemoved} function is called.
     */
    function onStrategyRemoved(address _strategy) override external onlyStEverVault {

        uint128 callBackMsgValue = 0.2 ever;

        delete strategies[_strategy];
        uint256 strategiesCount = strategies.keys().length;
        if (clusterState == ClusterState.REMOVING && strategiesCount == 0) {

            IStEverVault(stEverVault).onClusterRemoved{value: callBackMsgValue, bounce: false}(clusterOwner, _nonce);
            if (currentAssurance != 0) {
                address assuranceTo = isPunished ? stEverOwner : clusterOwner;
                TvmCell empty;
                // send remaining tokens
                ITokenWallet(stEverTokenWallet).transfer{value: callBackMsgValue, bounce: false}(
                    currentAssurance,
                    assuranceTo,
                    0,
                    assuranceTo,
                    false,
                    empty
                );
                // destroy wallet
                IDestroyable(stEverTokenWallet).destroy{value: callBackMsgValue, bounce: false}(assuranceTo);
            }
            // self destruct
            clusterOwner.transfer({
                value: 0,
                flag: MsgFlag.ALL_NOT_RESERVED + MsgFlag.DESTROY_IF_ZERO,
                bounce: false
            });
            return;
        }
        tvm.rawReserve(_reserve(), 0);
        clusterOwner.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }

    /**
     * @dev See {IStEverCluster-handleDelegateStrategies}.
     *
     * **Preconditions**:
     *
     *  - The caller must be the StEverVault contract.
     *
     * **Postconditions**:
     *
     *  - The strategies are added to the strategies mapping and the state is set to ACTIVE.
     */
    function handleDelegateStrategies(address[] _strategies) override external onlyStEverVault {
        tvm.rawReserve(_reserve(), 0);

        address[] handledStrategies;

        for (address strategy : _strategies) {

            if (!strategies.exists(strategy)) {
                strategies[strategy] = Strategy({
                        state: StrategyState.ACTIVE
                });
                deployedStrategies[strategy] = true;
                handledStrategies.push(strategy);
            }
        }

        IStEverVault(stEverVault).onStrategiesDelegationHandled{
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED,
            bounce: false
        }(clusterOwner, _nonce, handledStrategies);
    }

    /**
     * @dev See {IStEverCluster-handleUndelegateStrategies}.
     *
     * **Preconditions**:
     *
     *  - The caller must be the StEverOwner.
     *  - The cluster must not already be in the REMOVING state.
     *  - The value of the message must be greater than or equal to the required message value.
     *
     * **Postconditions**:
     *
     *  - The cluster state is set to REMOVING.
     *  - All strategies in the ACTIVE state are removed.
     */
    function dropCluster(bool _isPunish) override external onlyStEverOwner {

        require(clusterState != ClusterState.REMOVING, ErrorCodesCluster.CLUSTER_IS_REMOVING);

        isPunished = _isPunish;
        clusterState = ClusterState.REMOVING;

        address[] strategiesForRemove;

        for ((address strategy, Strategy strategyInfo) : strategies) {
            if (strategyInfo.state != StrategyState.ACTIVE) {
                continue;
            }
            strategiesForRemove.push(strategy);
        }

        require(
            msg.value >= strategiesForRemove.length * StEverVaultGas.REMOVE_STRATEGY_RESERVE,
            ErrorCodesCluster.NOT_ENOUGH_VALUE
        );

        _removeStrategies(strategiesForRemove);
    }

    /**
     * @dev See {IStEverCluster-getDetails}.
     */
    function getDetails() override external responsible returns (Details) {
        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false} Details(
            _nonce,
            stEverVault,
            stEverOwner,
            clusterOwner,
            requiredAssurance,
            maxStrategiesCount,
            currentVersion,
            strategyFactory,
            currentAssurance,
            uint32(strategies.keys().length),//currentStrategiesCount
            clusterState,
            isPunished,
            stEverTokenRoot,
            stEverTokenWallet
        );
    }

    /**
     * @dev Handles the acceptance of token transfers.
     * This function increases the current assurance by the amount of the transfer.
     * @param _amount The amount of the transfer.
     *
     * **Preconditions**:
     *
     *  - The caller must be the StEver token wallet.
     *
     * **Postconditions**:
     *
     *  - The current assurance is increased by the amount of the transfer.
     */
    function onAcceptTokensTransfer(
        address,
        uint128 _amount,
        address,
        address,
        address remainingGasTo,
        TvmCell
    ) override external {
        require(msg.sender == stEverTokenWallet, ErrorCodesCluster.NOT_ST_EVER_TOKEN_WALLET);
        tvm.rawReserve(_reserve(), 0);

        currentAssurance += _amount;

        emit AssuranceReceived(_amount, currentAssurance);

        remainingGasTo.transfer({value: 0, flag:MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }

    /**
     * @dev See {IStEverCluster-setAssurance}.
     *
     * **Preconditions**:
     *
     *  - The caller must be the StEverOwner.
     *  - The value of the message must be greater than or equal to the minimum call value.
     *
     * **Postconditions**:
     *
     *  - The required assurance is set to the specified value.
     */
    function setAssurance(uint128 _newRequiredAssurance) override external onlyStEverOwner minCallValue {
        tvm.rawReserve(_reserve(), 0);

        requiredAssurance = _newRequiredAssurance;

        emit SetAssuranceAmount(requiredAssurance);

        stEverOwner.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }

    /**
     * @dev Retrieves the free assurance.
     * @return uint128 The free assurance.
     */
    function freeAssurance() public view returns (uint128) {
        return currentAssurance >= requiredAssurance ? currentAssurance - requiredAssurance : 0;
    }

    /**
     * @dev See {IStEverCluster-withdrawAssurance}.
     *
     * **Preconditions**:
     *
     *  - The caller must be the clusterOwner.
     *  - The value of the message must be greater than or equal to the minimum call value.
     *
     * **Postconditions**:
     *
     *  - The specified amount of assurance is withdrawn, or the unlocked assurance if it is less.
     */
    function withdrawAssurance(uint128 _amount) override external onlyClusterOwner minCallValue {
        uint128 unlockedAssurance = freeAssurance();

        uint128 amountToWithdraw = math.min(_amount, unlockedAssurance);

        currentAssurance -= amountToWithdraw;

        tvm.rawReserve(_reserve(), 0);
        TvmCell empty;

        ITokenWallet(stEverTokenWallet).transfer{
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED,
            bounce: false
        }(
            amountToWithdraw,
            msg.sender,
            0,
            msg.sender,
            true,
            empty
        );

    }

    /**
     * @dev See {IStEverCluster-upgrade}.
     *
     * **Preconditions**:
     *
     *  - The caller must be the vault.
     *  - The new version must not be the same as the current version.
     *
     * **Postconditions**:
     *
     *  - The code is upgraded.
     */
    function upgrade(
        TvmCell _newCode,
        uint32 _newVersion,
        address _sendGasTo
    ) external virtual override onlyStEverVault {


        if (_newVersion == currentVersion) {
            tvm.rawReserve(_reserve(), 0);
            _sendGasTo.transfer({ value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED });
            return;
        }


        TvmBuilder mainBuilder;
        mainBuilder.store(stEverVault);
        mainBuilder.store(uint8(0));
        mainBuilder.store(_sendGasTo);

        TvmCell dummyPlatformCode;
        mainBuilder.store(dummyPlatformCode);


        TvmCell initialData = abi.encode(
            _nonce,
            stEverVault,
            clusterOwner
        );


        TvmCell constructorParams = abi.encode(
            _newVersion,
            currentVersion,
            requiredAssurance,
            maxStrategiesCount,
            strategyFactory,
            stEverTokenRoot,
            stEverOwner
        );

        mainBuilder.storeRef(initialData);
        mainBuilder.storeRef(constructorParams);
        uint32 currentStrategiesCount;
        TvmCell storageData = abi.encode(
            _nonce, //uint32
            stEverVault, //address
            stEverOwner, //address
            clusterOwner, //address

            requiredAssurance, //uint128
            maxStrategiesCount, //uint32
            currentVersion, //uint32
            strategyFactory, //address

            currentAssurance, //uint128
            currentStrategiesCount, //uint32
            clusterState, //ClusterState

            stEverTokenRoot, //address
            stEverTokenWallet, //address

            isPunished, //bool

            strategies, //mapping(address => Strategy)
            deployedStrategies //mapping(address => bool)
        );

        mainBuilder.storeRef(storageData);


        // set code after complete this method
        tvm.setcode(_newCode);
        // run onCodeUpgrade from new code
        tvm.setCurrentCode(_newCode);

        onCodeUpgrade(mainBuilder.toCell());
    }

    /**
     * @dev Called when the code is upgraded.
     * @param _upgradeData The data to use for the upgrade.
     *
     * **Postconditions**:
     *
     *  - The contract's storage is reset.
     *  - The gas is sent to the specified address.
     *  - If the new version is the same as the current version, the contract is initialized with the new version.
     *  - Otherwise, the contract's state is updated with the new version's data.
     */
    function onCodeUpgrade(TvmCell _upgradeData) private {
        tvm.resetStorage();
        tvm.rawReserve(_reserve(), 0);
        TvmSlice s = _upgradeData.toSlice();

        (,, address sendGasTo,) = s.decode(address,uint8,address, TvmCell);
        TvmCell initialData = s.loadRef();

        (
            _nonce,
            stEverVault,
            clusterOwner
        ) = abi.decode(
            initialData,
            (uint32, address, address)
        );

        TvmCell constructorParams = s.loadRef();

        (
            uint32 _current_version,
            uint32 _prevVersion,
            uint128 _assurance,
            uint32 _maxStrategiesCount,
            address _strategyFactory,
            address _stEverTokenRoot,
            address _stEverOwner
        ) = abi.decode(
            constructorParams,
            (
                uint32,
                uint32,
                uint128,
                uint32,
                address,
                address,
                address
            )
        );

        if (_current_version == _prevVersion) {
            _init(
                _current_version,
                _assurance,
                _maxStrategiesCount,
                _strategyFactory,
                _stEverTokenRoot,
                _stEverOwner
            );
        } else {
            TvmCell storageData = s.loadRef();
            currentVersion = _current_version;
            (
                _nonce,
                stEverVault,
                stEverOwner,
                clusterOwner,

                requiredAssurance,
                maxStrategiesCount,
                ,
                strategyFactory,

                currentAssurance,
                , //currentStrategiesCount
                clusterState,

                stEverTokenRoot,
                stEverTokenWallet,

                strategies,
                deployedStrategies
            ) = abi.decode(
            storageData,
                (
                uint32,
                address,
                address,
                address,

                uint128,
                uint32,
                uint32,
                address,

                uint128,
                uint32, // strategies count
                ClusterState,

                address,
                address,

                mapping(address => Strategy),
                mapping(address => bool)
                )
            );

        }


        sendGasTo.transfer({value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED});
    }
}
