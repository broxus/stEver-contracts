pragma ever-solidity >=0.62.0;
import "./StEverVaultBase.tsol";
import "../interfaces/IStEverCluster.tsol";


abstract contract StEverStrategiesManager is StEverVaultBase {

    /**
     * @dev Creates a cluster.
     * @param _clusterOwner The address of the cluster owner.
     * @param _assurance count of stEver tokens that should be provided by cluster owner.
     * @param _maxStrategiesCount The maximum number of strategies in the cluster.
     */
    function createCluster(
        address _clusterOwner,
        uint128 _assurance,
        uint32 _maxStrategiesCount
    ) override external onlyOwner {
        require(
            msg.value >= StEverVaultGas.DEPLOY_CLUSTER_VALUE + StEverVaultGas.MIN_CALL_MSG_VALUE,
            ErrorCodes.NOT_ENOUGH_VALUE
        );
        require(Utils.isValidAddress(strategyFactory), ErrorCodes.STRATEGY_FACTORY_DID_NOT_SET);
        tvm.rawReserve(_reserve(), 0);


        if (!clusterPools.exists(_clusterOwner)) {
            mapping(uint32 => address) emptyClusters;
            clusterPools[_clusterOwner] = ClustersPool({
                currentClusterNonce: 0,
                clusters: emptyClusters
            });
        } else {
            clusterPools[_clusterOwner].currentClusterNonce++;
        }

        address cluster = deployCluster(
            _clusterOwner,
            clusterPools[_clusterOwner].currentClusterNonce,
            _assurance,
            _maxStrategiesCount,
            strategyFactory,
            stTokenRoot,
            owner
        );

        clusterPools[_clusterOwner].clusters[clusterPools[_clusterOwner].currentClusterNonce] = cluster;
        emit ClusterCreated(_clusterOwner, _assurance, _maxStrategiesCount, cluster);
        owner.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }



    /**
     * @dev Removes a cluster.
     * @param _clusterOwner The address of the cluster owner.
     * @param _clusterNonce The nonce of the cluster.
     */
    function onClusterRemoved(
        address _clusterOwner,
        uint32 _clusterNonce
    ) override external onlyCluster(_clusterOwner, _clusterNonce) {
        delete clusterPools[_clusterOwner].clusters[_clusterNonce];
        if (clusterPools[_clusterOwner].clusters.keys().length == 0) {
            delete clusterPools[_clusterOwner];
        }
        emit ClusterRemoved(msg.sender, _clusterOwner, _clusterNonce);
    }

    /**
     * @dev Adds strategies to the cluster.
     * @param _strategies The addresses of the strategies.
     * @param _clusterOwner The address of the cluster owner.
     * @param _clusterId The id of the cluster.
     */
    function addStrategies(address[] _strategies, address _clusterOwner, uint32 _clusterId) override external onlyCluster(_clusterOwner, _clusterId)  {
        require (msg.value >= _strategies.length * StEverVaultGas.EXPERIMENTAL_FEE, ErrorCodes.NOT_ENOUGH_VALUE);

        uint8 batchSize = 50;

        require (_strategies.length <= batchSize, ErrorCodes.MAX_BATCH_SIZE_REACHED);


        for (address strategy : _strategies) {
            require (!strategies.exists(strategy), ErrorCodes.STRATEGY_ALREADY_EXISTS);

            strategies[strategy] = StrategyParams({
                    lastReport: 0,
                    totalGain: 0,
                    depositingAmount: 0,
                    withdrawingAmount: 0,
                    totalAssets: 0,
                    cluster: msg.sender,
                    state: StrategyState.ACTIVE
            });
        }

        tvm.rawReserve(_reserve(), 0);

        emit StrategiesAdded(_strategies);

        IStEverCluster(msg.sender).onStrategiesAdded{
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED,
            bounce: false
        }(_strategies);
    }

    /**
     * @dev Removes strategies from the cluster.
     * @param _strategies The addresses of the strategies.
     * @param _clusterOwner The address of the cluster owner.
     * @param _clusterId The id of the cluster.
     */
    function removeStrategies(
        address[] _strategies,
        address _clusterOwner,
        uint32 _clusterId
    ) override external onlyCluster(_clusterOwner, _clusterId) {

        address[] pendingDeleteStrategies;
        for (address _strategy : _strategies) {
            strategies[_strategy].state = StrategyState.REMOVING;

            if (strategies[_strategy].totalAssets == 0) {
                removeStrategy(_strategy);
                continue;
            }

            pendingDeleteStrategies.push(_strategy);
        }

        if (pendingDeleteStrategies.length == 0) {
            tvm.rawReserve(_reserve(), 0);
            _clusterOwner.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
            return;
        }

        uint128 minimalRemoveStrategyValue = uint128(pendingDeleteStrategies.length) * StEverVaultGas.REMOVE_STRATEGY_RESERVE;

        tvm.rawReserve(address(this).balance - (msg.value - minimalRemoveStrategyValue), 0);
        emit StrategiesPendingRemove(pendingDeleteStrategies);

        IStEverCluster(msg.sender).onPendingStrategyRemove{
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED,
            bounce: false
        }(pendingDeleteStrategies);
    }

    /**
     * @dev Removes a strategy.
     * @param _strategy The address of the strategy.
     */
    function removeStrategy(address _strategy) internal pure {
        this._removeStrategy{
            value: StEverVaultGas.REMOVE_STRATEGY_RESERVE,
            bounce: false
        }(_strategy);
    }

    /**
     * @dev Removes a strategy.
     * @param _strategy The address of the strategy.
     */
    function _removeStrategy(address _strategy) external override onlySelf {
        tvm.rawReserve(_reserve(), 0);

        StrategyParams strategy = strategies[_strategy];
        delete strategies[_strategy];
        emit StrategyRemoved(_strategy);

        IStEverCluster(strategy.cluster).onStrategyRemoved{
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED,
            bounce: false
        }(_strategy);
    }

    /**
     * @dev Delegates strategies to another cluster (function only for migration).
     * @param _strategies The addresses of the strategies.
     * @param _destinationCluster The address of the destination cluster.
     */
    function delegateStrategies(address[] _strategies, address _destinationCluster) override external onlyOwner {
        require(
            msg.value >= uint128(_strategies.length) * (StEverVaultGas.MIN_CALL_MSG_VALUE / 2),
            ErrorCodes.NOT_ENOUGH_VALUE
        );

        tvm.rawReserve(_reserve(), 0);

        address[] transferringStrategies;
        for (address _strategy : _strategies) {
            StrategyParams strategy = strategies[_strategy];
            if (strategy.state == StrategyState.ACTIVE && strategy.cluster == address(0)) {
                strategies[_strategy].state = StrategyState.TRANSFERRING;
                transferringStrategies.push(_strategy);
            }
        }

        IStEverCluster(_destinationCluster).handleDelegateStrategies{
                value: 0,
                flag: MsgFlag.ALL_NOT_RESERVED,
                bounce: false
        }(transferringStrategies);
    }

    /**
     * @dev Handles strategies delegation.
     * @param _clusterOwner The address of the cluster owner.
     * @param _clusterNonce The nonce of the cluster.
     * @param _strategies The addresses of handled the strategies.
     */
    function onStrategiesDelegationHandled(
        address _clusterOwner,
        uint32 _clusterNonce,
        address[] _strategies
    ) override external onlyCluster(_clusterOwner, _clusterNonce) {
        tvm.rawReserve(_reserve(), 0);

        for (address strategy : _strategies) {
            strategies[strategy].state = StrategyState.ACTIVE;
            strategies[strategy].cluster = msg.sender;
        }

        emit ClusterHandledStrategiesDelegation(msg.sender, _clusterOwner, _clusterNonce, _strategies);

        owner.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }
}
