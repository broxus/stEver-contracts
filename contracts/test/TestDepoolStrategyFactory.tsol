pragma ever-solidity >=0.62.0;
pragma AbiHeader expire;
pragma AbiHeader pubkey;

import "../interfaces/IDepoolStrategyFactory.tsol";
import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";
import "../StrategyDePool.tsol";
import "../interfaces/IStrategy.tsol";
import "../utils/Constants.tsol";
import "../utils/ErrorCodes.tsol";
import "../utils/Gas.tsol";



contract DepoolStrategyFactory is IDepoolStrategyFactory {
    // static
    uint128 public static nonce;
    TvmCell public static dePoolStrategyCode;
    address public static stEverVault;

    // state
    address owner;
    uint32 public strategyVersion;
    uint32 public strategyCount;
    uint32 public factoryVersion;

    // constant
    uint128 constant CONTRACT_MIN_BALANCE = 1 ever;
    uint128 constant UPGRADE_VALUE = 1 ever;
    uint128 constant STRATEGY_DEPLOY_VALUE = 2 ever;

    // errors
    uint8 constant NOT_OWNER = 101;
    uint8 constant LOW_MSG_VALUE = 102;
    uint8 constant WRONG_PUBKEY = 103;


    constructor(address _owner) public {
        require (tvm.pubkey() != 0, WRONG_PUBKEY);
        require (tvm.pubkey() == msg.pubkey(), WRONG_PUBKEY);

        tvm.accept();

        owner = _owner;
    }

    modifier onlyOwner() {
        require(msg.sender == owner,NOT_OWNER);
        _;
    }

    modifier minCallValue() {
        require (msg.value >= DePoolStrategyFactoryGas.MIN_CALL_MSG_VALUE, ErrorCodes.NOT_ENOUGH_VALUE_FACTORY);
        _;
    }
        // utils
    function _reserve() internal pure returns (uint128) {
		return
			math.max(address(this).balance - msg.value, CONTRACT_MIN_BALANCE);
	}

    function transferOwnership(address _newOwner, address _sendGasTo) override external onlyOwner {
        tvm.rawReserve(_reserve(), 0);

        owner = _newOwner;

        _sendGasTo.transfer({value: 0, flag:MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }

    function getDetails() override external responsible view returns (FactoryDetails) {
        return {value:0, bounce: false, flag: MsgFlag.REMAINING_GAS} FactoryDetails({
            stEverVault: stEverVault,
            owner: owner,
            strategyVersion: strategyVersion,
            strategyCount: strategyCount,
            factoryVersion: factoryVersion
        });
    }

    // test function
    function checkIsUpdateApplied() public view returns(FactoryDetails) {
       return FactoryDetails({
            stEverVault: stEverVault,
            owner: owner,
            strategyVersion: strategyVersion,
            strategyCount: strategyCount,
            factoryVersion: factoryVersion
        });
    }
    function installNewStrategyCode(TvmCell _strategyCode, address _sendGasTo) override external onlyOwner {
        require (msg.value >= UPGRADE_VALUE, LOW_MSG_VALUE);
        tvm.rawReserve(_reserve(),0);

        dePoolStrategyCode = _strategyCode;
        strategyVersion++;
        emit StrategyCodeUpdated(strategyVersion - 1,strategyVersion);

        _sendGasTo.transfer({value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED});
    }

    function deployStrategy(address _dePool) override external onlyOwner {
        require (msg.value >= STRATEGY_DEPLOY_VALUE, LOW_MSG_VALUE);
        tvm.rawReserve(_reserve(), 0);

        TvmCell stateInit = tvm.buildStateInit({
            contr: StrategyDePool,
            varInit: {
                nonce: strategyCount,
                factory: address(this),
                strategyVersion: strategyVersion
            },
            pubkey: tvm.pubkey(),
            code: dePoolStrategyCode
        });
        strategyCount++;
        address strategy = new StrategyDePool{
            stateInit: stateInit,
            value: 0,
            wid: address(this).wid,
            flag: MsgFlag.ALL_NOT_RESERVED
        }(stEverVault, _dePool);
        emit NewStrategyDeployed(strategy, _dePool, strategyVersion);
    }

    function upgradeStrategies(address[] _strategies) override external onlyOwner {
        require (_strategies.length <= Constants.MAX_STRATEGY_PER_UPGRADE, ErrorCodes.MAX_STRATEGY_THAN_ALLOWED);
        require (
            msg.value >= DePoolStrategyFactoryGas.MIN_CALL_MSG_VALUE * _strategies.length + DePoolStrategyFactoryGas.MIN_CALL_MSG_VALUE,
            ErrorCodes.NOT_ENOUGH_VALUE_FACTORY
        );

        tvm.rawReserve(_reserve(),0);

        for (address strategy : _strategies) {
            IStrategy(strategy).upgrade{value: DePoolStrategyFactoryGas.MIN_CALL_MSG_VALUE, bounce: false}(dePoolStrategyCode, strategyVersion, msg.sender);
        }

        msg.sender.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }


    function upgrade(TvmCell _newCode, uint32 _newVersion, address _sendGasTo) override external minCallValue onlyOwner {
        if (_newVersion == factoryVersion) {
            tvm.rawReserve(_reserve(), 0);
            _sendGasTo.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce:false});
            return;
        }

        // should be unpacked in the same order!
        TvmCell data = abi.encode(
            _sendGasTo, // address
            _newVersion, // uint32
            dePoolStrategyCode, // TvmCell
            stEverVault, // address
            owner, // address
            strategyVersion, // uint32
            strategyCount, // uint32
            factoryVersion // uint32
        );

        // set code after complete this method
        tvm.setcode(_newCode);
        // run onCodeUpgrade from new code
        tvm.setCurrentCode(_newCode);

        onCodeUpgrade(data);
    }

    function onCodeUpgrade(TvmCell _upgradeData) private {
        tvm.rawReserve(_reserve(), 0);
        tvm.resetStorage();
        address _sendGasTo;
        (
            _sendGasTo,
            factoryVersion,
            dePoolStrategyCode,
            stEverVault,
            owner,
            strategyVersion,
            strategyCount,

        ) = abi.decode(_upgradeData, (address, uint32, TvmCell, address, address, uint32, uint32, uint32));

        _sendGasTo.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }

}
